* Helm at the Emacs
  :PROPERTIES:
  :categories: emacs,helm
  :date:     2015/02/04 08:47:40
  :updated:  2015/02/04 08:47:40
  :END:
I have written several ([[http://kitchingroup.cheme.cmu.edu/blog/2015/01/24/Anatomy-of-a-helm-source/][intro]], [[http://kitchingroup.cheme.cmu.edu/blog/2015/02/01/Handling-multiple-selections-in-helm/][multiple args]], [[http://kitchingroup.cheme.cmu.edu/blog/2015/02/03/helm-and-prefix-functions/][prefix args)]] times about using Helm in Emacs so far. Today, I want to share a way I use helm to get me where I want to be in Emacs for my daily activities. This came out of a desire to have single command that would give me a lot of options to open exactly the buffer/file I wanted when I need it. I call the command hotspots, and it is bound to f9 for me, so when I press f9 I get a helm buffer to select what I want from.

So, what kinds of things do I want. First, I want to be able to open my mail, calendar, News feed or agenda from this command. Second, I have a list of hotspots I developed using the code at http://ergoemacs.org/emacs/emacs_hotkey_open_file_fast.html, which I want easy access to. Third, I want to be able to open any org-file in my agenda list. Fourth, any bookmark I have, or to set a bookmark. Fifth, I want recent files as candidates. There is certainly some redundancy in their, but that is ok, it gets me where I want to be.

Here is the code that does that for me. There are six helm sources that provide candidates and actions.

#+BEGIN_SRC emacs-lisp
(defun hotspots ()
  "helm interface to my hotspots, which includes my locations,
org-files and bookmarks"
  (interactive)
  (helm :sources `(((name . "Mail and News")
		    (candidates . (("Mail" . (lambda ()
					       (if (get-buffer "*mu4e-headers*")
						   (progn
						     (switch-to-buffer "*mu4e-headers*")
						     (delete-other-windows))

						 (mu4e))))
				   ("Calendar" . (lambda ()  (browse-url "https://www.google.com/calendar/render")))
				   ("RSS" . elfeed)
				   ("Agenda" . (lambda () (org-agenda "" "w")))))
		    (action . (("Open" . (lambda (x) (funcall x))))))
		   ((name . "My Locations")
		    (candidates . (("master" . "~/Dropbox/org-mode/master.org")
				   (".emacs.d" . "~/Dropbox/kitchingroup/jmax" )
				   ("blog" . "~/blogofile-jkitchin.github.com/_blog/blog.org")
				   ("ese" . "~/Dropbox/books/ese-book/ese.org" )
				   ("passwords" . "~/Dropbox/org-mode/passwords.org.gpg")
				   ("Pycse" . "~/Dropbox/books/pycse/pycse.org")
				   ("references" . "~/Dropbox/bibliography/references.bib")
				   ("notes" . "~/Dropbox/bibliography/notes.org")
				   ("journal" . "~/Dropbox/org-mode/journal.org")
				   ("tasks" . "~/Dropbox/org-mode/tasks.org")))
		    (action . (("Open" . (lambda (x) (find-file x))))))

		   ((name . "My org files")
		    (candidates . ,(f-entries "~/Dropbox/org-mode"))
		    (action . (("Open" . (lambda (x) (find-file x))))))
		   helm-source-recentf
		   helm-source-bookmarks
		   helm-source-bookmark-set)))
#+END_SRC

Interesting to me is that there are not a lot of actions in here. I mostly use this command for navigation to various places. For example, I press f9, type meet, and I can quickly get to the meetings file in my agenda list, or I can type the first few letters of a student's name and open the org-file associated with them. Or I press f9 and go down an entry to open my calendar, etc... I find this enormously helpful because it opens these files no matter where I am in Emacs, and it relieves my mind from remembering where they are, or the keystrokes/commands to get to them.