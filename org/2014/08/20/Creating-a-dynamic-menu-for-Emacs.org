* Creating a dynamic menu for Emacs
  :PROPERTIES:
  :categories: emacs
  :date:     2014/08/20 20:26:18
  :updated:  2014/08/20 20:26:18
  :END:
I have an application where I want a dynamic menu in Emacs, e.g. the menu auto-updates as things change on your computer. Here is a prototype example. We will make a menu that shows entries for each file in the current directory, and opens that entry.

We start by creating a menu that is initially empty like this. This menu will be called "My Files" in the menu.

#+BEGIN_SRC emacs-lisp
(easy-menu-define jrk-menu global-map "MyMenu"
  '("My Files"))
#+END_SRC

#+RESULTS:

Next, we create this function which will create a submenu with a vector entry for each file in this directory.
#+BEGIN_SRC emacs-lisp
(defun get-menu ()
  (easy-menu-create-menu
   "Files"
   (mapcar (lambda (x)
	     (vector (file-name-nondirectory x)
		     `(lambda () (interactive) (find-file ,x) t)))
	   (f-glob "*"))))
#+END_SRC

#+RESULTS:
: get-menu

Next, we add the submenu. This is a one-time addition, which reflects the files in the directory at the time I ran this block.

#+BEGIN_SRC emacs-lisp
(easy-menu-add-item jrk-menu '() (get-menu))
#+END_SRC

#+RESULTS:

After you do that, the menu looks like this:

[[./Screen Shot 2014-08-20 at 8.14.00 PM.png]]

This menu is not yet dynamic. We need to create a function that can update the menu, and then add the function to a hook that runs each time the menu opens. Here is the code. The easy-menu-add-item function will replace the contents of an item by the same name, which we use here to update the menu.

#+BEGIN_SRC emacs-lisp
(defun update-my-file-menu ()
  (easy-menu-add-item jrk-menu '() (get-menu)))

(add-hook 'menu-bar-update-hook 'update-my-file-menu)
#+END_SRC

#+RESULTS:
| update-my-file-menu | undo-tree-update-menu-bar | menu-bar-update-buffers |


Now, if we run this block to create a file:

#+BEGIN_SRC sh
touch newfile
#+END_SRC

#+RESULTS:

Then, after saving this buffer the menu looks like this:


[[./Screen Shot 2014-08-20 at 8.17.50 PM.png]]

Now, every time a new file appears in this directory, a new menu item will appear every time you check the menu. That is really dynamic.


